// Every line is (a bit) bananas!
// Implementation of single bit-variables operations

// should be assembled with an (exactly one) libXX.fjm file
// This file is independent of the bit-width, and uses the consts defined at libXX.fjm

// Everything after // is ignored, everything before first : is ignored too, and every whitespace is ignored (besides new line)
// An opcode is:  F;L  F  ;L  or just  ;  (no F => F=temp;  no L => L=next)

// Complexity note: phi is log2 of the total number of fj operations, counting ..flip_by* as one.


// TODO - implement pointer dereference (read and write)



// ---------- Compilation Time:


.def comp_if expr l0 l1
    w#temp
    w#(expr ? l1 : l0)
.end



// ---------- Memory Variables:


.def bit0
    ;0
.end

.def bit1
    ;dw
.end



// ---------- Memory Manipulation:


.def zero x
    .xor x x
.end


.def one x
    .zero x
    .not x
.end


////Complexity: 2phi + 9
//.def __if_mov dst src : l0 l1 flip end
//    .if src l0 l1
//    l0: .if dst end flip
//    l1: .if dst flip end
//    flip:   .not dst
//    end:
//.end


// safe even for dst==src !
// Complexity: 2phi + 5
.def mov dst src : do_mov end
    .comp_if dst-src end do_mov
  do_mov:
    .zero dst
    .xor dst src
  end:
.end


//Complexity: 2phi + 10
.def swap a b : a0 a1 notnot end
    .if a a0 a1
    a0: .if b end flip
    a1: .if b flip end
    notnot: .not a
            .not b
    end:
.end



// ---------- Unconditional Jump


.def skip
    ;$ + dw
.end


.def loop
    ;$ - dw
.end



// ---------- Conditional Jump


// Complexity: phi+4
.def if x l0 l1 : label_ptr base_jump_label
    .xor label_ptr x
  label_ptr:
    ;base_jump_label
    ..pad 2
  base_jump_label:
    ;l0
    label_ptr + dbit;l1
.end

.def if1 x l1 : end
    .if x end l1
  end:
.end

.def if0 x l0 : end
    .if x l0 end
  end:
.end


.def cmp a b lt eq gt : a_is1_label
    .if1 a a_is1_label
    .if b eq lt
  a_is1_label:
    .if b gt eq
.end



// ---------- Logical Macros:


// Complexity: phi+2
.def xor dst src
    .naked_xor dst + dbit src
.end

.def naked_xor dst src : base_jump_label cleanup
    ..flip_by_dbit src+w base_jump_label
    ;src
    ..pad 2
  base_jump_label:
    ;cleanup
    dst;cleanup
  cleanup:
    ..flip_by_dbit src+w base_jump_label
.end


.def or dst src : end
    .if0 src end
    .one dst
  end:
.end


.def and dst src : end
    .if1 src end
    .zero dst
  end:
.end


.def not dst
    dst + dbit;
.end



// ---------- Arithmetical Macros


// Unsafe for dst==carry (but there is no reason in calling it that way)
.def inc1 dst carry : end
    .if0 carry end
    .not dst
    .if0 dst end
    .not carry
  end:
.end


// Unsafe for dst==carry (but there is no reason in calling it that way)
.def add1 dst src carry : _src
    .mov _src src
    .inc1 dst _src
    .inc1 dst carry
    .or carry _src
    .skip
  _src: .bit0
.end
