// ---------- Logical Macros


ns bit {
    // Complexity: @-1
    //   dst ^= src
    def xor dst, src {
        .exact_xor dst + dbit, src
    }

    // Complexity: n(@-1)
    //   dst[:n] ^= src[:n]
    def xor n, dst, src {
        rep(n, i) .xor dst+dw*i, src+dw*i
    }

    // Complexity: @-1
    // note: pad 2 is needed, but pad 8 is used for wflips-padding optimization and for smaller wflips.
    //   dst(bit_address) ^= src
    def exact_xor dst, src @ base_jump_label, cleanup {
        wflip src+w, base_jump_label, src
        pad 8
      base_jump_label:
        ;cleanup
        dst;
      cleanup:
        wflip src+w, base_jump_label
    }

    // Complexity: @
    // note: pad 2 is needed, but pad 8 is used for wflips-padding optimization and for smaller wflips.
    //   dst1(bit_address) ^= src
    //   dst2(bit_address) ^= src
    def double_exact_xor dst1, dst2, src @ base_jump_label, cleanup {
        wflip src+w, base_jump_label, src
        pad 8
      base_jump_label:
        ;cleanup
        dst1;
        dst2;
      cleanup:
        wflip src+w, base_jump_label
    }

    // Complexity: n@
    //   bit(bit_address) ^= src
    //   var ^= src
    def bit_var_xor n, bit, var, src {
        rep(n, i) .double_exact_xor bit+i, var+dbit+i*dw, src+i*dw
    }

    // Complexity: n@
    //   dst ^= src
    //   src = 0
    def xor_zero dst, src {
        .double_exact_xor dst+dbit, src+dbit, src
    }

    // Complexity: n@
    //   dst[:n] ^= src[:n]
    //   src[:n] = 0
    def xor_zero n, dst, src {
        rep(n, i) .xor_zero dst+dw*i, src+dw*i
    }


    // Complexity: 2@+2
    //   dst |= src
    def or dst, src @ end {
        .if0 src, end
        .one dst
      end:
    }

    // Complexity: n(2@+2)
    //   dst[:n] |= src[:n]
    def or n, dst, src {
        rep(n, i) .or dst+dw*i, src+dw*i
    }


    // Complexity: 2@+2
    //   dst &= src
    def and dst, src @ end {
        .if1 src, end
        .zero dst
      end:
    }

    // Complexity: n(2@+2)
    //   dst[:n] &= src[:n]
    def and n, dst, src {
        rep(n, i) .and dst+dw*i, src+dw*i
    }


    // Complexity: 1
    //   dst ^= 1
    def not dst {
        dst + dbit;
    }

    // Complexity: n
    //   dst[i] ^= (1<<n)-1
    def not n, dst {
        rep(n, i) .not dst+dw*i
    }

    // Complexity: 1
    //   dst(bit_address) ^= 1
    def exact_not dst {
        dst;
    }
}
